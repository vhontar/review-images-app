plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlinx-serialization'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.vhontar.reviewimagesapp"
        minSdk 23
        targetSdk 32

        def majorVersion = 1
        def minorVersion = 0
        def patchVersion = 0

        // you might be wanted to put larger constants for a production app
        versionCode majorVersion * 100000 + minorVersion * 10000 + patchVersion * 1000
        versionName "$majorVersion.$minorVersion.$patchVersion"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.3.0'
        kotlinCompilerVersion kotlin_version
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    buildTypes {
        debug {
            minifyEnabled false

            // params from local.properties
            buildConfigField "String", "PIXABAY_URL", "\"${properties.getProperty('pixabay.url')}\""
            buildConfigField "String", "PIXABAY_KEY", "\"${properties.getProperty('pixabay.key')}\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // params from local.properties
            buildConfigField "String", "PIXABAY_URL", "\"${properties.getProperty('pixabay.url')}\""
            buildConfigField "String", "PIXABAY_KEY", "\"${properties.getProperty('pixabay.key')}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.paging:paging-common:3.1.1"
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'

    // navigation
    def nav_version = "2.5.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'

    // viewmodel and livedata
    def lifecycle_version = '2.5.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // room
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // network
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:4.10.0"

    // deserializer/serializer
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0"

    // Hilt dependencies
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}